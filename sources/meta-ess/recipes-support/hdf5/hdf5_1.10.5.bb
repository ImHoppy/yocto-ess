LICENSE = "BSD"
LIC_FILES_CHKSUM = "file://COPYING;md5=106bcadf06f1d36b417baffe40ded960"

BBCLASSEXTEND = "native"

SRC_URI = "https://support.hdfgroup.org/ftp/HDF5/releases/hdf5-1.10/hdf5-1.10.5/src/hdf5-1.10.5.tar.bz2"

# force shared library build
SRC_URI += "file://${PV}/0002-Add-configure-options-to-build-static-and-shared-lib.patch"

# avoid generating of the H5Tinit.c and H5lib_settings.c files
SRC_URI += "file://${PV}/0002-hdf5-c.patch"

#Add --with-c-max-real-precision and --enable-float-128
SRC_URI += "file://${PV}/0001-Add-new-option-for-c-max-real-precision-and-float-12.patch"

# H5Tinit.c and H5lib_settings.c files for each platform
SRC_URI += "${H5_CONFIG_FILES}"

H5_CONFIG_FILES ?= " \
"

H5_CONFIG_FILES_qoriq = " \
    file://${PV}/qoriq/H5Tinit.c \
    file://${PV}/qoriq/H5lib_settings.c \
"

H5_CONFIG_FILES_intel-x86-common = " \
    file://${PV}/cct/H5Tinit.c \
    file://${PV}/cct/H5lib_settings.c \
"

SRC_URI[md5sum] = "7c19d6b81ee2a3ba7d36f6922b2f90d3"
SRC_URI[sha256sum] = "68d6ea8843d2a106ec6a7828564c1689c7a85714a35d8efafa2fee20ca366f44"

DEPENDS += "zlib"
S = "${WORKDIR}/hdf5-1.10.5"
# Increment revision number if package changes...
PR = "r1"

EXTRA_OECONF = "--with-szlib=no --with-pthread=no --enable-threadsafe=no --enable-cxx=yes"
EXTRA_OECONF += "--with-c-max-real-precision=21 --enable-float-128=no"
EXTRA_OECONF += "--enable-shared=yes --enable-static=no"
EXTRA_OECONF += "hdf5_cv_fp_to_integer_overflow_works=yes"
EXTRA_OECONF += "hdf5_cv_ldouble_to_long_special=no"
EXTRA_OECONF += "hdf5_cv_long_to_ldouble_special=no"
EXTRA_OECONF += "hdf5_cv_ldouble_to_llong_accurate=yes"
EXTRA_OECONF += "hdf5_cv_llong_to_ldouble_correct=yes"
EXTRA_OECONF += "hdf5_cv_disable_some_ldouble_conv=no"

PROVIDES += "${PN}-tests ${PN}-tests-dbg ${PN}-examples"
PACKAGES += "${PN}-tests ${PN}-tests-dbg ${PN}-examples"

FILES_${PN} += "${libdir}/libhdf5.settings"
FILES_${PN}-tests = "${libdir}/hdf5-tests/[a-z]*"
FILES_${PN}-tests-dbg = "${libdir}/hdf5-tests/.debug"
FILES_${PN}-examples = "${datadir}/hdf5_examples"

inherit autotools pkgconfig

INSANE_SKIP_${PN}-tests += "already-stripped"
INSANE_SKIP_${PN} += "already-stripped"

# add files generated by HDF5 during compilation; these cannot be
# generated during cross-compilation
do_configure_append_qoriq() {
    cp ${WORKDIR}/${PV}/qoriq/H5*.c ${B}/src/
}

do_configure_append_intel-x86-common() {
    cp ${WORKDIR}/${PV}/cct/H5*.c ${B}/src/
}

do_install_append() {
    install -d ${D}/${libdir}/hdf5-tests
    cp -r ${B}/test/.libs/* ${D}/${libdir}/hdf5-tests/
    rm ${D}/${libdir}/hdf5-tests/*.a
    rm ${D}/${libdir}/hdf5-tests/*.o
    rm ${D}/${libdir}/hdf5-tests/*.la
}
